model{

    # Priors

    # Host Community
    for(i in 1:n_host){

        # covariate effects on occurrence, and intercept
        for(j in 1:n_w){
            c[i,j] ~ dnorm(mu_c[j], prec_c[j])
        } #j

        # covariate effects on detection, and intercept
        for(j in 1:n_o){
            d[i,j] ~ dnorm(mu_d[j], prec_d[j])
        } #j

    } #i

    # Hyperpriors for Occupancy
    for(i in 1:n_w){

        mu_c[i] ~ dnorm(0, 0.1)
        prec_c[i] <- pow(sd_c[i], -2)
        sd_c[i] ~ dt(0, 2.25, 1)T(0, )

    }

    # Hyperpriors for Detection
    for(i in 1:n_o){

        mu_d[i] ~ dnorm(0, 0.1)
        prec_d[i] <- pow(sd_d[i], -2)
        sd_d[i] ~ dt(0, 2.25, 1)T(0, )

    }

    # Cowbird
    # Occupancy
    for(i in 1:n_v){
        a[i] ~ dnorm(0, 0.2)
    }

    # Detection
    for(i in 1:n_u){
        b[i] ~ dnorm(0, 0.2)
    }

    # Models
    for (i in 1:n_site){
        for(j in 1:n_host){

            # Host Community
            # Occupancy
            logit(eta[i, j]) <- inprod( t(W[i, ]), c[j, ])
            tau[i, j] ~ dbern(eta[i, j])

            # Detection
            for(k in 1:n_visit){

                logit(zeta[i, j, k]) <- inprod(t(O[, i, k]), d[j, ])
                X[i, j, k] ~ dbern(tau[i, j] * zeta[i, j, k])

                X.new[i, j, k] ~ dbern(tau[i, j] * zeta[i, j, k])

            } # k
        } # i

        # Cowbird
        # Occupancy
        logit(psi[i]) = inprod( t(V[i, ]), a[])
        z[i] ~ dbern(psi[i])

        # Detection
        for (k in 1:n_visit){

            logit(theta[i, k]) = inprod( t(U[, i, k]), b[])
            Y[i, k] ~ dbern(theta[i, k] * z[i])

            Y.new[i, k] ~ dbern(theta[i, k] * z[i])

        } # k

    } # j

    # Predictions
    # Probability of occurrence for cowbird by landuse
    psi.for <- ilogit(a[1])
    psi.cof <- ilogit(a[1] + a[2])

    for (i in 1:n_host){

        # Probability of occurrence for host by land use
        eta.for[i] <- ilogit(c[i, 1])
        eta.sha[i] <- ilogit(c[i, 1] + c[i, 2])
        eta.sun[i] <- ilogit(c[i, 1] + c[i, 3])

        # Probability of co-occurrence for host/cowbird by land use
        coc.for[i] <- psi.for * eta.for[i]
        coc.sha[i] <- psi.cof * eta.sha[i]
        coc.sun[i] <- psi.cof * eta.sun[i]

    } #i

    # Number of sites by true occurrence / co-occurrence state
    for (j in 1:n_host){
        for(i in 1:n_site){

            forest[i,j] <- (W[i, 1] - W[i, 2] - W[i, 3])

            # number of sites with both species by land use
            both_for[i,j] = z[i] * tau[i,j] * forest[i,j]
            both_sha[i,j] = z[i] * tau[i,j] * W[i, 2]
            both_sun[i,j] = z[i] * tau[i,j] * W[i, 3]

            # number of sites with just host by land use
            only_host_for[i,j] = (1 - z[i]) * tau[i,j] * forest[i,j]
            only_host_sha[i,j] = (1 - z[i]) * tau[i,j] * W[i, 2]
            only_host_sun[i,j] = (1 - z[i]) * tau[i,j] * W[i, 3]

        }

        n_both_for[j] = sum(both_for[, j])
        n_both_sha[j] = sum(both_sha[, j])
        n_both_sun[j] = sum(both_sun[, j])

        n_only_for[j] = sum(only_host_for[, j])
        n_only_sha[j] = sum(only_host_sha[, j])
        n_only_sun[j] = sum(only_host_sun[, j])

    }

    n_sites_w_two <- sum(W[,2])
    n_sites_w_three <- sum(W[,3])

}
